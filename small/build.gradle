buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
    }
}

apply from: 'base.gradle'

//--------------------------------------------------------------------------------------------------
// Scripts to install plugin to local repository [~/.m2/repository]
// > ./gradlew install
//--------------------------------------------------------------------------------------------------
task install(type: GradleBuild) {
    buildFile = 'install.gradle'
    tasks = ['install']
}

//--------------------------------------------------------------------------------------------------
// Scripts to upload plugin to Bintray/JCenter [https://bintray.com/]
// > ./gradlew bintrayUpload
//--------------------------------------------------------------------------------------------------
task bintrayUpload(type: GradleBuild) {
    buildFile = 'publish.gradle'
    tasks = ['bintrayUpload']
}

task doc(type: GradleBuild) {
    buildFile = 'publish.gradle'
    tasks = ['javadoc']
}

//--------------------------------------------------------------------------------------------------
// Helper scripts
//--------------------------------------------------------------------------------------------------
task changes << {
    def changelog = project.file('CHANGELOG.md')
    if (!changelog.exists()) return
    def br = new BufferedReader(new FileReader(changelog))
    def top = br.readLine()
    br.close()

    def loc = top.indexOf('(')
    if (loc < 0) return
    def date = top.substring(loc + 1)
    loc = date.indexOf(')')
    date = date.substring(0, loc)

    def log = "git log --since=$date --oneline ${project.projectDir}"
    println log.execute().text
}