//--------------------------------------------------------------------------------------------------
// Scripts to install plugin to local repository [~/.m2/repository]
// > ./gradlew install
//--------------------------------------------------------------------------------------------------
apply plugin: 'groovy'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

apply from: 'provided.gradle'

apply plugin: 'maven'

//--------------------------------------------------------------------------------------------------
// Scripts to upload plugin to Bintray/JCenter [https://bintray.com/]
// > ./gradlew bintrayUpload
//--------------------------------------------------------------------------------------------------
task bintrayUpload(type: GradleBuild) {
    buildFile = 'bintray.gradle'
    tasks = ['bintrayUpload']
}

//--------------------------------------------------------------------------------------------------
// Scripts to upload plugin to Gradle Plugins Portal [https://plugins.gradle.org/]
// > ./gradlew publish
//--------------------------------------------------------------------------------------------------
task publish(type: GradleBuild) {
    buildFile = 'publish.gradle'
    tasks = ['build']
}

//--------------------------------------------------------------------------------------------------
// Helper scripts
//--------------------------------------------------------------------------------------------------
task changes << {
    def changelog = project.file('CHANGELOG.md')
    if (!changelog.exists()) return
    def br = new BufferedReader(new FileReader(changelog))
    def top = br.readLine()
    br.close()

    def loc = top.indexOf('(')
    if (loc < 0) return
    def date = top.substring(loc + 1)
    loc = date.indexOf(')')
    date = date.substring(0, loc)

    def log = "git log --since=$date --oneline ${project.projectDir}"
    println log.execute().text
}
